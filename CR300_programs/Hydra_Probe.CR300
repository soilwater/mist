'Title: Connecting Hydraprobe to a CR300 datalogger
'Date: April 21, 2022
'Author: Nathaniel Parker and Andres Patrignani
'Correspondence: andrespatrignani@ksu.edu; parkernath32@ksu.edu

'Wiring:
'Red -> Power (12 V+)
'Blue -> Signal (C1)
'Black -> Ground (G)
'Note: Make sure to connect a power source (battery) to the datalogger
'Also, make sure to change the sensor address if necessary (this code uses "0" as the sensor address).


'Declare Variables and Units
Public BattV
Public PTemp_C
Public Hydraprobe(7)
Alias Hydraprobe(1)=VWC
Alias Hydraprobe(2)=EC_corrected 'corrected with temperature
Alias Hydraprobe(3)=Temp_Deg_C
Alias Hydraprobe(4)=Temp_Deg_F
Alias Hydraprobe(5)=EC_uncorrected
Alias Hydraprobe(6)=Perm_real
Alias Hydraprobe(7)=Perm_imaginary

Units BattV=Volts
Units PTemp_C=Deg C
Units VWC=m^3 /m^3
Units EC_corrected=S/m
Units Temp_Deg_C=Deg C
Units Temp_Deg_F=Deg F
Units EC_uncorrected=S/m
Units Perm_real=unitless
Units Perm_imaginary=unitless

'Define Data Tables
DataTable(Table1,True,-1)
	DataInterval(0,60,Sec,10)
	Sample(1,BattV,FP2)
	Sample(1,PTemp_C,FP2)
	Average(1,VWC,FP2,False)
	Average(1,EC_corrected,FP2,False)
	Average(1,Temp_Deg_C,FP2,False)
	Average(1,Temp_Deg_F,FP2,False)
	Average(1,EC_uncorrected,FP2,False)
	Average(1,Perm_real,FP2,False)
	Average(1,Perm_imaginary,FP2,False)
EndTable

DataTable(Table2,True,-1)
	DataInterval(0,1440,Min,10)
	Minimum(1,BattV,FP2,False,False)
EndTable

'Main Program
BeginProg
	'Main Scan
	Scan(10,Sec,1,0)
		'Default CR300 Datalogger Battery Voltage measurement 'BattV'
		Battery(BattV)
		'Default CR300 Datalogger Processor Temperature measurement 'PTemp_C'
		PanelTemp(PTemp_C,60)
		SDI12Recorder(Hydraprobe(),C1,"0","M!",1,0,-1)
		'Reset all Hydraprobe measurements if NAN is returned to Hydraprobe(1)
		If Hydraprobe(1)=NAN Then Move(Hydraprobe(),7,NAN,1)
		'Call Data Tables and Store Data
		CallTable Table1
		CallTable Table2
	NextScan
EndProg
