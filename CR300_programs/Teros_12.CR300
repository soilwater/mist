'Title: Connecting Teros 12 to a CR300 datalogger
'Date: April 11, 2022
'Author: Nathaniel Parker and Andres Patrignani
'Correspondence: andrespatrignani@ksu.edu

'Wiring:
'Brown -> Power (12 V+)
'Orange -> Signal (C1)
'Bare -> Ground (G)
'Note: Make sure to connect a power source (battery) to the datalogger
'Also, make sure to change the sensor address if necessary (this code uses "a" as the sensor address).

'Declare Variables and Units
Public BattV
Public PTemp_C
Public VWC
Public Perm
Public Teros12(3)
Alias Teros12(1)=RAW
Alias Teros12(2)=Temp
Alias Teros12(3)=EC

Units BattV = Volts
Units PTemp_C = Deg C
Units RAW = Unitless
Units VWC = m^3/m^3
Units Perm = Unitless
Units Temp = Deg C
Units EC = dS/m

'Define Data Tables
DataTable(Table1,True,-1)
	DataInterval(0,60,Sec,10)
	Average(1,VWC,FP2,False)
	Average(1,Perm,FP2,False)
	Average(1,Temp,FP2,False)
	Average(1,EC,FP2,False)
	Average(1,RAW,FP2,False)
EndTable

DataTable(Table2,True,-1)
	DataInterval(0,1440,Min,10)
	Minimum(1,BattV,FP2,False,False)
EndTable

'Main Program
BeginProg
	'Main Scan
	Scan(10,Sec,1,0)
		'Default CR300 Datalogger Battery Voltage measurement 'BattV'
		Battery(BattV)
		'Default CR300 Datalogger Processor Temperature measurement 'PTemp_C'
		PanelTemp(PTemp_C,60)
		'Teros 12 measurements
    SDI12Recorder(Teros12(),C1,"a","M!",1,0,-1)
		'Reset all Teros 12 Sensor measurements if NAN is returned to Teros12(1)
		If Teros12(1)=NAN Then Move(Teros12(),3,NAN,1)
		'Calculate volumetric water content
    VWC = (3.879*10^-4)*RAW - 0.6956
    'Calculate apparent dielectric permittivity
    Perm = ((2.887*10^-9)*RAW^3 - (2.080*10^-5)*RAW^2 + (5.276*10^-2)*RAW -43.39)^2
		'Call Data Tables and Store Data
		CallTable Table1
		CallTable Table2
	NextScan
EndProg
